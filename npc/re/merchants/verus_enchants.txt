//===== rAthena Script =======================================
//= Enchants NPCs.
//===== Description: =========================================
//= [Official Conversion]
//= Excellion Wing/Suit trader and enchant NPCs
//===== Changelogs: ==========================================
//= 1.0 First Version. [Capuche]
//============================================================

verus04,161,222,4	script	Grandpa picking up scrap iron	1_M_HOF,{
	switch( isbegin_quest(12368) ) {
	case 0:
		mes "[Grandpa picking up scrap iron]";
		mes "There are two pieces of scrap iron I see the value of not Fibonacci to pick up and sell to the recycling bin, the result is not willing to deal with me.";
		next;
		mes "[Grandpa picking up scrap iron]";
		mes "Because it's not iron? Each resource recycling are not willing to buy, is nerve-racking!";
		close;
	case 1:
		mes "[Grandpa picking up scrap iron]";
		mes "Hey! Did you just talk to that piece of junk? Or am I losing my sight?";
		close;
	case 2:
		mes "[Grandpa picking up scrap iron]";
		mes "Oh, my God! That can't be sold to the Recycle Bin!";
		close;
	}
}

verus04,163,219,4	script	PLUTO_09#pa0829	4_SCR_AT_ROBOTS,{
	disable_items;
	if (checkweight(1201,1) == 0) {
		mes "^ff0000You have carried too many items, please reduce it and come back later.^000000";
		close;
	}
	if ((MaxWeight - Weight) < 10000) {
		mes "^ff0000You have carried too many items, please reduce it and come back later.^000000";
		close;
	}
	if (isbegin_quest(12368) < 2) {
		mes "^0000ffAlthough the LCD still dim, but does not seem to work anymore.^000000";
		close;
	}
	mes "^0000ffWhen the strange dinosaur pattern appears, go to the introduction screen.^000000";
	next;
	mes "[PLUTO_09]";
	mes "" + strcharinfo(0) + ",";
	mes "Hello. How can I help you?";
	next;
	switch( select( "Note", "Old Fuel and Huge Metal Scrap to exchange for gear.", "Design drawings for the production of Enchantment" ) ) {
	case 1:
		mes "^0000ffDinosaur forward screen appears and a description of the cute girl voice.^000000";
		next;
		mes "[PLUTO_09]";
		mes "This is 100 years since the genesis of the new PLUTO_09.";
		next;
		mes "[PLUTO_09]";
		mes "Bring Huge Metal Scrap and Old Fuel can help you make useful to you Equipment Oh~";
		next;
		mes "[PLUTO_09]";
		mes "Do you want to make enchantment equipment? You bring the tank! I can make enchant with a design for you. Oh, good luck.";
		next;
		mes "[PLUTO_09]";
		mes "This is the Tyrannosaurus rex that appeared in the dinosaur fairy tale, very beautiful... (Continue to appear boring dinosaur story)";
		next;
		mes "^0000ffIn this introduction to the screen, presumed that the psychological state of the robot maker is extremely unstable.^000000";
		close;
	case 2:
		mes "[PLUTO_09]";
		mes "Production equipment needs ^0000ff10 Huge Metal Scrap and 5 Old Fuel^000000.";
		next;
		switch( select( "Quit.", "Making the Excelion Wing", "Making the Excelion Suit" ) ) {
		case 1:
			mes "^0000ffDinosaurs gazed at the picture here after staying a bit closed.^000000";
			close;
		case 2:
			.@item_id = 20773; // Excelion_Wing
			break;
		case 3:
			.@item_id = 15128; // Excelion_Suit
			break;
		}
		if (countitem(6961) < 10 || countitem(6962) < 5) {
			mes "[PLUTO_09]";
			mes "I have already talked about the production equipment needs ^0000ff10 Huge Metal Scrap and 5 Old Fuel^000000, you don't have enough.";
			close;
		}
		specialeffect EF_SPELLBREAKER;
		delitem 6962,5;// OldTank
		delitem 6961,10;// LargeScrap
		getitem .@item_id,1;
		mes "[PLUTO_09]";
		mes "Your equipment is ready. Let's see if it fits.";
		close;
	case 3:
		while(1) {
			if (countitem(6962) < 5)
				.@string$ = "^aaaaaaInsufficient fuel for making design drawings.^000000";
			else
				.@string$ = "Production design (^ff0000 success rate 30%^000000)";
			mes "[PLUTO_09]";
			mes "If I give the Old Fuel 5, I can do the production of design drawings look!";
			mes "----------";
			mes "^0000Little dinosaur running around on the screen.^000000";
			next;
			if (select( "Quit.", .@string$ ) == 1) {
				mes "^0000ffIt's over after showing that the dinosaurs are stuffing themselves.^000000";
				close;
			}
			.@fail = false;
			.@reac_rnd = rand(1,10000);
			if (.@reac_rnd < 26)		.@en_name = 6977;	// Reactor_A_STR_
			else if (.@reac_rnd < 51)	.@en_name = 6978;	// Reactor_A_INT_
			else if (.@reac_rnd < 251)	.@en_name = 6979;	// Reactor_A_DEF_
			else if (.@reac_rnd < 326)	.@en_name = 6981;	// Reactor_A_ATK_
			else if (.@reac_rnd < 401)	.@en_name = 6982;	// Reactor_A_MATK_
			else if (.@reac_rnd < 601)	.@en_name = 6980;	// Reactor_A_AVOI_
			else if (.@reac_rnd < 801)	.@en_name = 6983;	// Reactor_A_MHP_
			else if (.@reac_rnd < 1001)	.@en_name = 6984;	// Reactor_A_MSP_
			else if (.@reac_rnd < 1151)	.@en_name = 6986;	// Reactor_A_ASPD_
			else if (.@reac_rnd < 1401)	.@en_name = 6975;	// Reactor_Cure_201_
			else if (.@reac_rnd < 1551)	.@en_name = 6976;	// Reactor_Cure_202_
			else if (.@reac_rnd < 1751)	.@en_name = 6973;	// Reactor_Cure_101_
			else if (.@reac_rnd < 1851)	.@en_name = 6974;	// Reactor_Cure_102_
			else if (.@reac_rnd < 1901)	.@en_name = 6985;	// Reactor_A_FROZ_
			else if (.@reac_rnd < 2101)	.@en_name = 6970;	// Reactor_T_WATER_
			else if (.@reac_rnd < 2301)	.@en_name = 6971;	// Reactor_T_GROUND_
			else if (.@reac_rnd < 2501)	.@en_name = 6969;	// Reactor_T_FIRE_
			else if (.@reac_rnd < 2701)	.@en_name = 6972;	// Reactor_T_WIND_
			else if (.@reac_rnd < 2776)	.@en_name = 6966;	// Reactor_P_WATER_
			else if (.@reac_rnd < 2851)	.@en_name = 6967;	// Reactor_P_GROUND_
			else if (.@reac_rnd < 2926)	.@en_name = 6965;	// Reactor_P_FIRE_
			else if (.@reac_rnd < 3001)	.@en_name = 6968;	// Reactor_P_WIND_
			else {
				.@fail = true;
				.@reac_rnd2 = rand(1,13000);
				if (.@reac_rnd2 < 2)
					.@en_name = 969;	// Gold
				else if (.@reac_rnd2 < 1002)
					.@en_name = 999;	// Steel
				else if (.@reac_rnd2 < 3002)
					.@en_name = 998;	// Iron
				else if (.@reac_rnd2 < 8002)
					.@en_name = 1002;	// Iron_Ore
				else
					.@en_name = 7054;	// Brigan
			}
			if (countitem(6962) < 5)
				mes "^0000ffThe picture shows the dinosaur silently looking at the empty bowl, it may be an error message when the material is insufficient!^000000";
			else {
				specialeffect EF_SPELLBREAKER;
				delitem 6962,5;// OldTank
				getitem .@en_name,1;
				mes "[PLUTO_09]";
				if (.@fail)
					mes "What a pity, the design failed.";
				else
					mes "Great, the design was made.";
			}
			next;
		}
		end;
	default:
		mes "^0000ffA screen showing howling that a dinosaur may not understand the command.^000000";
		close;
	}
	end;
}

verus04,165,217,4	script	MARS_01#pa0829	4_SCR_MT_ROBOTS,{
	disable_items;
	if (checkweight(1201,1) == 0) {
		mes "^ff0000You have carried too many items, please reduce it and come back later.^000000";
		close;
	}
	if ((MaxWeight - Weight) < 10000) {
		mes "^ff0000You have carried too many items, please reduce it and come back later.^000000";
		close;
	}
	switch( isbegin_quest(12368) ) {
	case 0:
		mes "Blurred LCD screen may be frightened or happy and intense shaking, perhaps due to lack of power and can not complete the presentation.";
		next;
		if (select( "Just ignore it.", "Press every button." ) == 1) {
			mes "It may be a pre-scrap robot that is common everywhere and doesn't require much attention.";
			close;
		}
		mes "[?????]";
		mes "Hello! I'm MARS_01, an exploration robot. Currently, I have entered hibernation mode to save power. The related functions are set as follows.";
		next;
		if (select( "Interrupt the machine", "Supply of fuel" ) == 1) {
			mes "The robot re-enters hibernation mode after a faint sound.";
			close;
		}
		mes "[MARS_01]";
		mes "We are super power robot, as long as the old oil 1 will let me and the companion PLUTO has more than 90% of the power.";
		next;
		mes "[MARS_01]";
		mes "If you find Old Fuel, please insert the fuel into the mouth of the bottom of the screen, so you can lift the hibernation mode.";
		setquest 12368;	// Operate the old robot
		close;
	case 1:
		if (countitem(6962) < 1) {
			mes "[MARS_01]";
			mes "If you find Old Fuel, please insert the fuel into the mouth of the bottom of the screen, so you can lift the hibernation mode.";
			close;
		}
		mes "Maybe we ran out of power when we went looking for fuel. should we put in the fuel?";
		next;
		if (select( "Quit.", "Put in the fuel" ) == 1) {
			mes "The old robot may be running out of power and not responding at all.";
			close;
		}
		mes "When the fuel is put in, the sound screen opens.";
		npctalk "Whoa, whoa?! It moves!!!", "Grandpa picking up scrap iron";
		next;
		mes "[MARS_01]";
		mes "Start the inspection system and confirm the damage of each part.";
		next;
		mes "[MARS_01]";
		mes "Power meter 10%, dashboard normal, body skeleton 13%, perform mining operations well.";
		next;
		mes "[MARS_01]";
		mes "Pluto_09 began to inject energy for mining auxiliary robot.";
		npctalk "Energy response confirmed. It's working.", "PLUTO_09#pa0829";
		next;
		mes "[MARS_01]";
		mes "Thank you for getting me started, I will reset the person who started me to be a manager.";
		next;
		select("Enter a name.");
		mes "[MARS_01]";
		mes "" + strcharinfo(0) + ",";
		mes "Hello! The fuel has been fully charged.";
		delitem 6962,1;// OldTank
		completequest 12368;
		close;
	case 2:
		break;
	}
	mes "[MARS_01]";
	mes "" + strcharinfo(0) + ",";
	mes "Hello! Can I help you?";
	next;
	switch( select( "Note", "Enhanced axelion propulsion wing", "Enhanced Ike selion jacket" ) ) {
	case 1:
		mes "^0000ffSoon the instructions again.^000000";
		next;
		mes "[MARS_01]";
		mes "While waiting for the new owner, we continue to update to the latest version.";
		next;
		mes "[MARS_01]";
		mes "If you bring aak Seri Wong propulsion wing, aak Seri Wong jacket equipment and strengthen the design, I will according to the design to strengthen the equipment.";
		next;
		mes "[MARS_01]";
		mes "According to the standard design drawings of the operation, and will not damage the equipment, but according to the design and the number of upgrades will be changed.";
		next;
		mes "[MARS_01]";
		mes "Welcome back again~";
		close;
	case 2:
		.@part = EQI_GARMENT;
		break;
	case 3:
		.@part = EQI_ARMOR;
		break;
	}
	// <item ID required>, <item enchant ID>, <max number of this enchant on armor>, <max number of this enchant on garment>, <enchant on first slot only>
	setarray .@list[0],
		6965, 4970, 1,0,3,	// Reactor_P_FIRE_		Reactor_P_FIRE
		6966, 4971, 1,0,3,	// Reactor_P_WATER_		Reactor_P_WATER
		6967, 4972, 1,0,3,	// Reactor_P_GROUND_	Reactor_P_GROUND
		6968, 4973, 1,0,3,	// Reactor_P_WIND_		Reactor_P_WIND
		6969, 4974, 0,3,0,	// Reactor_T_FIRE_		Reactor_T_FIRE
		6970, 4975, 0,3,0,	// Reactor_T_WATER_		Reactor_T_WATER
		6971, 4976, 0,3,0,	// Reactor_T_GROUND_	Reactor_T_GROUND
		6972, 4977, 0,3,0,	// Reactor_T_WIND_		Reactor_T_WIND
		6973, 4978, 3,3,0,	// Reactor_Cure_101_	Reactor_Cure_101
		6974, 4979, 3,3,0,	// Reactor_Cure_102_	Reactor_Cure_102
		6975, 4980, 3,3,0,	// Reactor_Cure_201_	Reactor_Cure_201
		6976, 4981, 3,3,0,	// Reactor_Cure_202_	Reactor_Cure_202
		6977, 4982, 1,0,0,	// Reactor_A_STR_		Reactor_A_STR
		6978, 4983, 1,0,0,	// Reactor_A_INT_		Reactor_A_INT
		6979, 4984, 3,3,0,	// Reactor_A_DEF_		Reactor_A_DEF
		6980, 4985, 1,1,0,	// Reactor_A_AVOI_		Reactor_A_AVOI
		6981, 4986, 3,3,0,	// Reactor_A_ATK_		Reactor_A_ATK
		6982, 4987, 3,3,0,	// Reactor_A_MATK_		Reactor_A_MATK
		6983, 4988, 3,3,0,	// Reactor_A_MHP_		Reactor_A_MHP
		6984, 4989, 3,3,0,	// Reactor_A_MSP_		Reactor_A_MSP
		6985, 4990, 1,0,0,	// Reactor_A_FROZ_		Reactor_A_FROZ
		6986, 4991, 1,1,0;	// Reactor_A_ASPD_		Reactor_A_ASPD
	.@size = getarraysize(.@list);

	for ( .@i = 0; .@i < .@size; .@i += 5 ) {
		if (countitem(.@list[.@i]) < 1)
			.@menu$ += sprintf( "^aaaaaa%s (Missing)^000000:", getitemname(.@list[.@i]) );
		else
			.@menu$ += sprintf( "%s:", getitemname(.@list[.@i]) );
	}
	mes "[MARS_01]";
	mes "" + strcharinfo(0) + ",";
	mes "Choose the enchant you want to upgrade!";
	next;
	.@s = select("Quit.:" + .@menu$) - 2;
	if (.@s < 0) {
		mes "[MARS_01]";
		mes "So far.";
		close;
	}
	.@s *= 5;
	.@item_req = .@list[.@s];
	.@item_enchant_id = .@list[.@s+1];
	.@first_slot_only = .@list[.@s+4];

	.@equip_id = getequipid(.@part);
	.@equip_refine = getequiprefinerycnt(.@part);
	setarray .@card[0], getequipcardid(.@part,0), getequipcardid(.@part,1), getequipcardid(.@part,2), getequipcardid(.@part,3);

	if (countitem(.@item_req) < 1) {
		mes "[MARS_01]";
		mes "So far.";
		close;
	}
	if (.@part == EQI_ARMOR)
		.@max_num_enchant = .@list[.@s+2];
	else if (.@part == EQI_GARMENT)
		.@max_num_enchant = .@list[.@s+3];
	else {
		mes "[MARS_01]";
		mes "Please contact the administrator.";
		close;
	}

	// anti-hack
	if (callfunc("F_IsEquipIDHack", .@part, .@equip_id) || callfunc("F_IsEquipCardHack", .@part, .@card[0], .@card[1], .@card[2], .@card[3]) || callfunc("F_IsEquipRefineHack", .@part, .@equip_refine)) {
		mes "[MARS_01]";
		mes "Did you remove your gear?";
		close;
	}

	if (.@card[0] > 0 && .@item_enchant_id == .@card[0]) { .@slot_sum++; }
	if (.@card[1] > 0 && .@item_enchant_id == .@card[1]) { .@slot_sum++; }
	if (.@card[2] > 0 && .@item_enchant_id == .@card[2]) { .@slot_sum++; }
	if (.@card[3] > 0 && .@item_enchant_id == .@card[3]) { .@slot_sum++; }

	if (.@max_num_enchant < 1) {
		mes "[MARS_01]";
		mes "The design you choose is incompatible with the equipment.";
		close;
	}
	if (.@first_slot_only > 0 && .@card[3] != 0) {
		mes "[MARS_01]";
		mes "This design is only for the first time to strengthen the use, but the equipment has other performance, please use other design to strengthen it!";
		close;
	}
	if (.@slot_sum >= .@max_num_enchant) {
		mes "[MARS_01]";
		mes "The maximum number of enchants is " + .@max_num_enchant + ". The item has reached the upper limit.";
		close;
	}
	if (.@card[1] != 0) {
		mes "[MARS_01]";
		mes "The number of equipment has reached the limit.";
		close;
	}

	// anti-hack
	if (callfunc("F_IsEquipIDHack", .@part, .@equip_id) || callfunc("F_IsEquipCardHack", .@part, .@card[0], .@card[1], .@card[2], .@card[3]) || callfunc("F_IsEquipRefineHack", .@part, .@equip_refine)) {
		mes "[MARS_01]";
		mes "Please pay special attention if the equipment will be removed.";
		close;
	}

	switch( .@equip_id ) {
	case 20773:	// Excelion_Wing
	case 15128:	// Excelion_Suit
		break;
	default:
		mes "[MARS_01]";
		mes "The product does not have a serial number, does not meet the specifications of the product can not be strengthened.";
		close;
	}
	mes "[MARS_01]^0000ff";
	mes "Choose " + getequipname(.@part) + " + " + getitemname(.@item_req) + ",";
	mes "^000000------------------";
	mes "Your selected design can be upgraded to the equipment limit ^0000ff" + .@max_num_enchant + " the same design, ^000000so far, enhanced ^0000ff" + .@slot_sum + " times^000000, do you want to continue?";
	next;
	if (select( "I'll think about it...", "Go on." ) == 1) {
		mes "[MARS_01]";
		mes "Come back next time you need me~";
		close;
	}

	// anti-hack
	if (callfunc("F_IsEquipIDHack", .@part, .@equip_id) || callfunc("F_IsEquipCardHack", .@part, .@card[0], .@card[1], .@card[2], .@card[3]) || callfunc("F_IsEquipRefineHack", .@part, .@equip_refine))
		close;

	if (.@card[3] == 0) {
		.@enchant_count = 1;
		.@card[3] = .@item_enchant_id;
	}
	else if (.@card[2] == 0) {
		.@enchant_count = 2;
		.@card[2] = .@item_enchant_id;
	}
	else if (.@card[1] == 0) {
		.@enchant_count = 3;
		.@card[1] = .@item_enchant_id;
	}
	else {
		mes "[MARS_01]";
		mes "The equipment has reached the upper limit of strengthening Hello!";
		close;
	}
	specialeffect2 EF_REPAIRWEAPON;
	mes "[MARS_01]";
	if (.@enchant_count < 5)
		mes "The first equipment ^990000" + .@enchant_count + " times^000000 upgrade.";
	else
		mes "For additional performance equipment^990000 ^000000 upgrade.";	// skipped

	delitem .@item_req, 1;
	delequip .@part;
	getitem2 .@equip_id,1,1,.@equip_refine,0,.@card[0],.@card[1],.@card[2],.@card[3];
	close;
}
